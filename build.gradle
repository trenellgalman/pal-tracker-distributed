import io.pivotal.pal.tracker.gradlebuild.DependenciesGraphPlugin

buildscript {
    ext {
        springBootVersion = "2.4.2"
        springVersion = "5.3.3"
        mysqlVersion = "8.0.23"
        jacksonVersion = "2.12.1"
        slf4jVersion = "1.7.30"
        mockitoVersion = "3.7.7"
        assertJVersion = "3.18.1"
        hikariVersion = "3.4.5"
        logbackVersion = "1.2.3"
        junitVersion = "5.7.0"
        okhttpVersion = "4.9.0"
        jsonPathVersion = "2.5.0"
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "mysql:mysql-connector-java:$mysqlVersion"
    }
}

apply plugin: DependenciesGraphPlugin

subprojects {
    group "io.pivotal.pal.tracker"

    apply plugin: "java"
    defaultTasks "clean", "build"

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        implementation("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
        implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
        implementation("com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion")
        implementation("org.slf4j:slf4j-api:$slf4jVersion")

        testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
        testImplementation("org.mockito:mockito-core:$mockitoVersion")
        testImplementation("org.mockito:mockito-junit-jupiter:$mockitoVersion")
        testImplementation("org.assertj:assertj-core:$assertJVersion")
    }

    test {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}
